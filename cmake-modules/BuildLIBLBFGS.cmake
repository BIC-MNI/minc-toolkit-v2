macro(build_liblbfgs install_prefix staging_prefix)
  
  if(CMAKE_EXTRA_GENERATOR)
    set(CMAKE_GEN "${CMAKE_EXTRA_GENERATOR} - ${CMAKE_GENERATOR}")
  else()
    set(CMAKE_GEN "${CMAKE_GENERATOR}")
  endif()
  
  set(CMAKE_OSX_EXTERNAL_PROJECT_ARGS)
  if(APPLE)
    list(APPEND CMAKE_OSX_EXTERNAL_PROJECT_ARGS
      -DCMAKE_OSX_ARCHITECTURES:STRING=${CMAKE_OSX_ARCHITECTURES}
      -DCMAKE_OSX_SYSROOT:STRING=${CMAKE_OSX_SYSROOT}
      -DCMAKE_OSX_DEPLOYMENT_TARGET:STRING=${CMAKE_OSX_DEPLOYMENT_TARGET}
    )
  endif()

  ExternalProject_Add(LIBLBFGS
    SOURCE_DIR LIBLBFGS
    BINARY_DIR LIBLBFGS-build
    URL "https://github.com/vfonov/liblbfgs/archive/v1.10-cmake.tar.gz"
    URL_MD5 ""
    CMAKE_GENERATOR ${CMAKE_GEN}
    CMAKE_ARGS
        -DBUILD_TESTING:BOOL=OFF #${BUILD_TESTING}
        -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
        -DBUILD_SHARED_LIBS:BOOL=OFF
        -DCMAKE_INSTALL_PREFIX:PATH=${install_prefix}
        ${CMAKE_OSX_EXTERNAL_PROJECT_ARGS}
        "-DCMAKE_CXX_FLAGS:STRING=-fPIC ${CMAKE_CXX_FLAGS}"
        "-DCMAKE_C_FLAGS:STRING=-fPIC ${CMAKE_C_FLAGS}"
        -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS}
        -DCMAKE_MODULE_LINKER_FLAGS:STRING=${CMAKE_MODULE_LINKER_FLAGS}
        -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS}
        -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
        -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    INSTALL_COMMAND $(MAKE) install DESTDIR=${staging_prefix}
    INSTALL_DIR ${staging_prefix}/${install_prefix}
  )
  
SET(LIBLBFGS_LIB_SUFFIX ".a")

SET(LIBLBFGS_INCLUDE_DIR  ${staging_prefix}/${install_prefix}/include )
SET(LIBLBFGS_LIBRARY_DIR  ${staging_prefix}/${install_prefix}/lib${LIB_SUFFIX}/ )
SET(LIBLBFGS_LIBRARY      ${staging_prefix}/${install_prefix}/lib${LIB_SUFFIX}/liblbfgs${LIBLBFGS_LIB_SUFFIX} )
SET(LIBLBFGS_FOUND        TRUE)
 

endmacro(build_liblbfgs)
